plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {

    namespace "com.sovworks.eds.android"
    compileSdk 34

    defaultConfig {
        applicationId "com.sovworks.eds.android"
        minSdk 21
        targetSdk 34
        versionCode 237
        versionName "2.0.0.237"
        multiDexEnabled true


        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

	signingConfigs {
        debug {
            storeFile file('../misc/keys.keystore')
            storePassword 'android'
            keyAlias 'eds-debug-key'
            keyPassword 'android'
        }
    }    

    flavorDimensions = ["fullOrLite", "licCheckType", "inet", "fsm"]

    productFlavors {
        
        lite {
            dimension "fullOrLite"
            applicationId "com.sovworks.edslite"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-lite.pro'
            externalNativeBuild {
                cmake {
                    targets "edsxts", "edsctr", "edsaes", "edsserpent", "edstwofish", "edsripemd160", "edswhirlpool", "fdraio", "localxts", "edscfb", "edscbc", "edsctr", "edsgost"
                }
            }
        }
        licCheckNone {
            dimension "licCheckType"
        }
        noinet {
            dimension "inet"
        }
        nofsml {
            dimension "fsm"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if ((names.contains("lite") && !names.contains("licCheckNone")) ||
                (names.contains("full") && names.contains("licCheckNone")) ||
                (names.contains("inet") && !names.contains("full")) ||
                (names.contains("fsm") && !names.contains("full")) ||
                (names.contains("nofsm") && !names.contains("full")) ||
                (names.contains("fsm") && !names.contains("inet")) ||
                (names.contains("fsm") && !names.contains("licCheckGP")) ||
                (names.contains("nofsml") && !names.contains("lite"))

        ) {
            setIgnore(true)
        }
    }

    buildTypes {
        
        debug {
            signingConfig signingConfigs.debug
            externalNativeBuild {

                cmake {
                    cFlags "-DDEBUG"
                }
            }
            debuggable true
            jniDebuggable true
        }
    }

    packaging {
        resources {
            excludes += '/META-INF/LICENSE.txt'
            excludes += '/META-INF/LICENSE'
            excludes += '/META-INF/NOTICE.txt'
            excludes += '/META-INF/NOTICE'
        }
    }

    lint {
          abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = outputFileName.replace("app-", "EDS-").replace(".apk", "-v${variant.versionName}.apk")
        }
    }

    externalNativeBuild {

        cmake {
           path "CMakeLists.txt"
        }
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }

    // WICHTIG: Mit Kotlin 2.x + compose plugin i.d.R. nicht mehr nötig/gewünscht
    // composeOptions {
    //     kotlinCompilerExtensionVersion '1.5.10'
    // }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'com.drewnoakes:metadata-extractor:2.19.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    constraints {
        implementation('androidx.core:core:1.13.1') {
            because '1.17.0 requires compileSdk 36'
        }
        implementation('androidx.core:core-ktx:1.13.1') {
            because '1.17.0 requires compileSdk 36'
        }
    }
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    // implementation 'androidx.core:core-ktx:1.17.0'  // entfernt, weil compileSdk 36 + AGP 8.9.1 nötig wäre

    def compose_version = '1.6.2'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.material3:material3:1.2.0"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.activity:activity-compose:1.8.2'

    implementation 'androidx.exifinterface:exifinterface:1.3.7'

    // implementation 'com.github.str4d:age-java:0.2.0'
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    testImplementation 'junit:junit:4.13.2'
}


apply from: 'build-full.gradle'

if (file('build-local').exists()) {
	apply from: 'build-local.gradle'
}
